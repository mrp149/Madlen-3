C+HISTORY   08/08/81  17.27.15
C+HISTORY   28/07/81  12.06.06
C+HISTORY   23/07/81  10.24.31
C+HISTORY   21/07/81  19.05.24
C+HISTORY   21/07/81  12.31.44
C+HISTORY   21/07/81  07.36.49
C+HISTORY   18/07/81  17.43.58
C+HISTORY   17/07/81  15.42.11
C+HISTORY   09/07/81  07.17.14
C+HISTORY   04/07/81  19.15.31
C+HISTORY   26/06/81  14.33.08
C+HISTORY   18/06/81  08.07.57
C+HISTORY   04/06/81  16.26.17
C+HISTORY   02/06/81  07.57.21
C
C+HISTORY   01/06/81  05.25.17
C+HISTORY   30/05/81  13.35.59
C+HISTORY   29/05/81  01.35.10
C+HISTORY   27/05/81  06.05.38
 MDLCARD*:,NAME,
 W16:,EQU,16B
  W17:,EQU,17B
C*    COMMON БЛOKИ MONITOR*
C
  MONCARD*:,LC,25
 ENTRY*:LC,BLOCK,PASS,INFWORD,INPUT,PAGE
 ,CONT,MODEL,DISP,EDITF
 ,CONT,LISTF,XTEXTF,SNAME(8)
 TRNAME*:,LC,
  ARREAD*:,LC,
 KCOUNT*:,LC,
 MONREAD*:,SUBP,
 PRINT8:,SUBP,
 MONCARD/:,LC,14
 *MONCRD*:,LC,12
 OUT*OUT:,LC,1
 STEXTBF*:PC,BLOCK,IDTAB(4096),MACTAB(1024),OUTBF(1024)
 DECFL:,P/P,(OUTBF-IDTAB)1024
 OUTPT:,EQU,OUTBF-IDTAB
 NDEC:,P/P,(IDTAB)16
 NOUT:,P/P,(OUTBF)16
 PARAMT*:,SUBP,
 TEXISO:,SUBP,
 *1:B,BLOCK,TYPPAR,DEFPAR,DEFRES
 14,VTM,-6
 :14,ITS,7
 14,VLM,*
 ,XTS,
 ,XTA,MAINOVL
 ,ATX,TRNAME*
 14,VTM,-41
 ,XTA,
 :14,ATX,ENTRY*+41
 14,VLM,*
 ,XTA,PASSWORD
 ,ATX,PASS
 13,MTJ,3
 2,VJM,READCARD
C
C*    ФOPMИPOBAHИE KOHCTAHT ЗAГPYЗKИ
C
  INMEM:,BSS,
  ,XTA,W17
 ,ATX,R10
 ,ARX,RCON10
 ,ATX,W17
C
C*******  ЗAГPYЗKA CИCTEMHOГO TEKCTA
C
 STEXTLDR:,WTC,STEXTPTR
 14,VTM,1
 ,XTA,BIT25
 ,ARX,STEXTCNT
 ,ATX,STEXTCNT
 14,XTA,
 ,ATX,NAME
 ,UZA,SYSLD
 2,VTM,
 ,ITA,14
 ,ATX,STEXTPTR
 ,*70,46B
 ,XTA,R10
 ,WTC,R10
 ,ATX,2
 ,*70,61B
 ,XTA,20B
 ,ATX,S20
 ,XTA,JMP20
 ,CTX,20B
 ,XTS,NAME
 ,WTC,43B
 13,VJM,2.CALL REDUOL
 ,XTA,S20
 ,CTX,20B
 ,WTC,R10
 4,VTM,8
 ,XTA,W17
 ,AAX,CONR15
 ,ATX,L
C
C*******  OБPAБOTKA CИCTEMHOГO TEKCTA
C
 13,VJM,READWORD
 ,AEX,TESTST
 ,U1A,FLIGHT+1
 13,VTM,MAKEDATE
 14,VJM,READWORD
 12,VTM,COMMENT
 13,VJM,READWORD
 :12,ATX,
 ,AAX,CONR8
 ,UZA,LOADSYS1
 12,VLM,READWORD
 LOADSYS1:13,VTM,PRISTEXT
 ,ITS,13
 ,ITS,12
 ,XTS,WOPAR
 ,CALL,PRINT8
 SYSLOOP:13,VJM,READWORD
 ,ATI,2
 ,ASN,64+15
 ,AAX,RCON3
 ,ATI,14
 14,UJ,*+1
 :,UJ,SYSGLOB
 :13,VTM,SYSID
 ,UJ,SYSMAC
 :13,VTM,SYSMAC0
 ,UJ,SYSMAC
 :,UJ,SYSLOOP
 JMP20:,UJ,*+1
 :,XTA,S20
 ,CTX,20B
 ,UJ,STEXTLDR
C
C+  OБPAБOTKA ГPYППЫ  -G L O B A L-
C
 SYSGLOB:,BSS,
 ,XTA,CM4
 14,ASN,64
 ,AOX,INFWORD
 ,AAX,C377
 4,AOX,-1
 ,ATX,INFWORD
 ,UJ,SYSLOOP
C
C+  OБPAБOTKA ГPYППЫ  -I D E N T-
C
 SYSID:2,UTM,-1
 13,VJM,READWORD
 ,ATX,TIDI
 2,V1M,SYSID1
C+  KOPOTKИE ИДEHTИФИKATOPЫ
 SYSID0:,XTA,TIDI
 ,AAX,CONR15
 ,ATX,SHIFS
 ,AEX,TIDI
 ,ATX,TIDI
 14,VJM,HASHID
 2,V1M,SYSID
 ,UJ,SYSLOOP
 SYSID1:4,XTA,
 ,AAX,C77
 ,U1A,SYSID0
C+  ДЛИHHЫE ИДEHTИФИKATOPЫ
 2,UTM,-1
 13,VJM,READWORD
 ,ATX,SHIFS
 ,XTA,TIDI
 14,VJM,HASHID
 2,V1M,SYSID
 ,UJ,SYSLOOP
C
C+  IDTAB  H A S H I N G
C
 HASHID:,ATX,IDI
 12,VTM,-3
 HASHID1:,AAX,CON7776
 ,XTS,IDI
 ,ASN,64-11
 ,ATX,IDI
 15,ARX,
 12,VLM,HASHID1
 ,ASN,64-12
 ,YTA,
 12,VTM,IDTAB
 ,ITS,12
 15,ARX,
 ,ATX,ENDST
 ,ATI,12
 HASHID2:12,XTA,1
 ,AEX,CONSS
 ,UZA,HASHIDS
 12,XTA,
 ,AEX,TIDI
 14,UZA,
 12,UTM,2
 13,VTM,-MACTAB
 12,J+M,13
 13,V1M,HASHID3
 12,VTM,IDTAB
 ,UJ,HASHID2
 HASHID3:,ITA,12
 ,AEX,ENDST
 ,U1A,HASHID2
 ,UJ,FLIGHT
 HASHIDS:,XTA,TIDI
 12,ATX,
 :,XTA,SHIFS
 ,AEX,STEXTCNT
 12,ATX,1
 14,UJ,
C
C+  OБPAБOTKA ГPYППЫ -M A C R O-
C
 SYSMAC:2,VZM,SYSLOOP
 ,XTA,CM4
 14,ASN,64
 ,AOX,INFWORD
 ,ATX,INFWORD
 13,UJ,
 SYSMAC0:2,UTM,-1
 13,VJM,READWORD
 ,ARX,
 ,UZA,SYSPUR
 13,VJM,SEARCHM
 2,UTM,-1
 13,VJM,READWORD
 ,ATX,SHIFS
 ,AAX,CM4
 ,UZA,SYSOPS
 ,XTA,SHIFS
 ,ASN,64-15
 ,AOX,OUTPNT
 12,ATX,1
 SYSMAC1:2,UTM,-1
 13,VJM,READWORD
 ,ATX,SHIFS
 13,VJM,PUTWORD
 ,XTA,SHIFS
 ,AEX,WOPAR
 ,UZA,SYSOPS+1
 ,UJ,SYSMAC1
 SYSOPS:,XTA,SHIFS
 12,ATX,1
 :2,V1M,SYSMAC0
 ,UJ,SYSLOOP
 SYSPUR:,AEX,CM4
 13,VJM,SEARCHM
 ,XTA,
 12,ATX,1
 :2,V1M,SYSMAC0
 ,UJ,SYSLOOP
 SYSLD:3,MTJ,13
 ,XTA,INFDEC
 ,AAX,MAS18
 ,AOX,INFWORD
 ,ATX,INFWORD
 12,VTM,1-DECFL
 SYSLD1:,*70,INFDEC
 ,XTA,INFDEC
 ,ARX,NEXTZ
 ,ATX,INFDEC
 12,VLM,SYSLD1
 ,XTA,OUTPNT
 ,AAX,CONR10
 ,UZA,RETURN
 ,XTA,OUTPNT
 ,ASN,64+10
 ,ARX,INFOUT
 ,ATX,INFOUT
 ,AAX,CHECKDR
 ,U1A,FLIGHT.DRUM FULL
 ,*70,INFOUT
 RETURN:14,VTM,
 15,XTA,
 :14,STI,7
 14,UTM,6
 13,UTC,
 14,VZM,
 14,UTM,-7
 ,UJ,RETURN+1
 SEARCHM:,ATX,SHIFS
 14,VTM,-511
 12,VTM,MACTAB
 SEARCHM0:12,XTA,
 ,UZA,SEARCHM1
 ,AEX,SHIFS
 13,UZA,
 12,UTM,2
 14,VLM,SEARCHM0
 FLIGHT:11,VTM,SOLONG.DROP STEXT
 ,UJ,ERRORF+1
 :,XTA,
 ,ATX,BADT+4
 ,ATX,BADT+5
  ,XTS,STEXTPTR
 14,VTM,BADT+4
 ,ITS,14
 ,CALL,TEXISO
 :11,VTM,BADT
 ,UJ,ERRORF+1
 SEARCHM1:,XTA,SHIFS
 12,ATX,
 13,UJ,
 PUTWORD:,XTS,OUTPNT
 ,AAX,CONR10
 ,STI,12
 ,UTC,OUTBF
 12,ATX,
 12,UTM,-1777B
 12,V1M,PUTWORD1
 ,XTA,OUTPNT
 ,ASN,64+10
 ,ARX,INFOUT
 ,ATX,SHIFS
 ,AAX,CHECKDR
 ,U1A,FLIGHT.DRUM FULL
 ,*70,SHIFS
 PUTWORD1:,BSS,
 ,XTA,OUTPNT
 ,ARX,WOPAR
 ,ATX,OUTPNT
 13,UJ,
 READWORD:,ITA,4
 ,AEX,L
 ,UZA,*+2
 4,XTA,
 4,UTM,1
 13,UJ,
 :2,V1M,FLIGHT+1
 ,UJ,STEXTLDR
 MAKEDATE:,ITS,14
 ,ITS,13
 ,ITS,12
 14,VTM,NAME
 ,ITS,14
 14,VTM,PRICOM+1
 ,ITS,14
 ,CALL,TEXISO
 ,STI,12
 ,STI,13
 ,STI,14
 ,ATX,IDI
 ,AUX,XX/YY
 ,AOX,DATE
 ,ATX,PRICOM+3
 ,XTA,IDI
 ,ASN,64-14
 ,AUX,/ZZ
 ,AOX,DATE+1
 ,ATX,PRICOM+4
 ,XTA,IDI
 ,ASN,64-22
 ,AUX,XX/YY
 ,AOX,TIME
 ,ATX,PRICOM+5
 ,XTA,IDI
 ,ASN,64-36
 ,AUX,/ZZ
 ,AOX,TIME+1
 ,ATX,PRICOM+6
 14,UJ,
C
C
C*******  OБPAБOTKA KAPTЫ *ASSEMBLER
C*    BOЗMOЖHЫE ФOPMЫ KAPTЫ *ASSEMBLER
C*
C*    *ASSEMBLER              /OLD
C*    *ASSEMBLER.KOMEHTAPИЙ   /NEW
C*    *ASSEMBLER(P1,P2...PN)  /NEW
C*    *ASSEMBLER,P1,P2...PN.  /NEW
C*
C*    ГДE P1-PN MOГYT ИMETЬ BИД
C*    KEYWORD=YYYYYY
C*    KEYWORD=XXXXXX
C*     **  KEYWORD  **
C*    MODEL  MOДEЛЬ ДЛЯ  SSW   ( M )
C*    LIST   LISTING CONTROL   ( L )
C*    EDIT   EDITING CONTROL   ( E )
C*    STEXT  CИCTEMHЫЙ TEKCT   ( S )
C*    XTEXT  BHEШHИЙ  TEKCT    ( X )
C*    INPUT  ФAЙЛ BBOДA        ( I )
C*    DISP   ИMЯ ДИCПETЧEPA    ( D )
C*
C*    XXXXXX  HAБOP ИЗ БYKB,ЦИФP,*,/,$
C*    YYYYYY  HAБOP ИЗ 8-HЫX ЦИФP
C
C
 READCARD:,BSS,
 ,XTA,WOPAR
 ,UTC,OUT*OUT
 ,ATX,
 10,VTM,
 9,VTM,
 ,CALL,PARAMT*
 11,VZM,SETPAR
 PARLOOP:10,VTM,3
 9,VTM,
 ,CALL,PARAMT*
 OPSWITCH:11,VZM,ENDCARD
 11,UTM,-54B .,
 7,VTM,PARLOOP
 ,XTS,WOPAR
 ,STX,COMMFLG
 11,VZM,DEFAULT
 ,XTS,
 ,STX,COMMFLG
 11,UTM,54B-75B .=
 7,VTM,RECOVERY
 11,VZM,GETPAR
 11,UTM,75B-51B .)
 7,VTM,SETPAR
 11,VZM,DEFAULT
 11,UTM,51B-56B ..
 11,VZM,DEFAULT
 ,UJ,BADDEL
 FRSWITCH:11,VZM,ENDCARD2
 11,UTM,-54B .,
 11,VZM,SETCOMM
 11,UTM,54B-51B .)
 11,VZM,ENDCARD2
 11,UTM,51B-56B ..
 11,VZM,ENDCARD2
 ,UJ,BADDEL
 :15,XTA,
 DEFAULT:14,V1M,BADPAR
 7,UZA,
 13,VJM,SEARCHP
 14,XTA,TYPPAR
 ,AAX,USEBIT
 ,U1A,SEARCHP1
 14,XTA,TYPPAR
 ,AOX,USEBIT
 14,ATX,TYPPAR
 14,XTA,DEFPAR
 14,ATX,DEFRES
 7,UJ,
 SETCOMM:,XTA,WOPAR
 ,ATX,COMMFLG
 ,UJ,PARLOOP
 RECOVERY:,XTA,COMMFLG
 ,U1A,PARLOOP
 10,VTM,1
 9,VTM,
 ,CALL,PARAMT*
 ,UJ,FRSWITCH
  GETPAR:14,V1M,BADPAR
   ,UZA,BADPAR
 13,VJM,SEARCHP
 14,XTA,TYPPAR
 ,AAX,USEBIT
 ,U1A,SEARCHP1
 14,XTA,TYPPAR
 ,AOX,USEBIT
 14,ATX,TYPPAR
 ,ATI,13
 14,WTC,TYPPAR
 ,XTA,PARTYPE-1
 ,ATI,10
 14,MTJ,9
 9,UTM,DEFRES
 7,VTM,FRSWITCH
 13,UTM,PARJMP
 ,UJ,PARAMT*
C*
C*    TAБЛИЦA COOTBETCTBИЯ TИПA ПAPAMETPA
C*    И TИПA PARATM*. TИП RARAMT* XPAHИTCЯ
C*    B MAДШИX БИTAX PARTYPE
C*
 PARJMP:,UJ,BADPAR .0
 PARTYPE:,UJ,MADEB .1
 ,,1
 :,UJ,MODPAR .2
 ,,1
 :,UJ,STEXTPAR .3
 ,,3
 :,UJ,FILEPAR .4
 ,,4
 :,UJ,PAGEPAR .5
 ,,7
 :,UJ,BADPAR
 ,,6
 :,UJ,BADPAR
 ,,7
 SEARCHP:,UTC,*MONCRD*
 ,XTA,
 ,ATX,DONT
 ,AEX,SP6
 ,UZA,BADPAR
 ,ARX,C6*177
 ,AAX,C6*200
 ,ASN,64+7
 ,ARX,C6*177
 ,AEX,C6*177
 ,ATX,MASK
 ,XTA,DONT
 ,APX,MASK
 ,ATX,DONT
 ,XTA,MASK
 ,APX,MASK
 ,ATX,MASK
 14,VTM,TABPAR
 SEARCHP0:14,XTA,
 ,UZA,BADPAR
 ,AEX,DONT
 ,AAX,MASK
 13,UZA,
 SEARCHP1:14,UTM,4
 ,UJ,SEARCHP0
 FILEPAR:14,V1M,BADARG
 ,UJ,FRSWITCH
 PAGEPAR:14,V1M,BADARG
 ,UJ,FRSWITCH
 MADEB:14,VTM,*MONCRD*
 13,VTM,-5
 ,XTA,
 9,ATX,
 MADEB1:14,XTA,
 ,UZA,FRSWITCH
 MADEB2:14,XTA,
 12,VZM,FRSWITCH
 12,UTM,-1
 ,ASN,64-8
 14,ATX,
 ,YTA,
 ,ATI,10
 ,AEX,RCON60
 ,UZA,SET0
 ,XTA,CON15
 10,ASN,
 9,AOX,
 9,ATX,
 13,VLM,MADEB2
 13,VTM,-5
 14,UTM,1
 ,UJ,MADEB1
 STEXTPAR:14,V1M,CHECK0
  9,ATX,
 ,U1A,FRSWITCH
 CHECK0:12,UTM,-1
 12,V1M,BADARG
 ,UTC,*MONCRD*
 ,XTA,
 ,ASN,64+40
 ,AEX,RCON60
 ,U1A,BADARG
 SET0:9,ATX,
 ,UJ,FRSWITCH
 MODPAR:,UTC,*MONCRD*
 ,XTA,
 ,ATX,PARAM
 14,VTM,-5
 MODPAR1:12,VZM,MODPAR3
  12,UTM,-1
 ,XTA,PARAM
 ,ASN,64-8
 ,ATX,PARAM
 ,YTA,
 9,XTS,
 ,ASN,64-8
 15,AEX,
 9,ATX,
 14,VLM,MODPAR1
 12,V1M,BADARG
 ,UJ,FRSWITCH
 MODPAR3:9,XTA,
 ,ASN,64-8
 ,AOX,STAR
 9,ATX,
 14,VLM,MODPAR3
 ,UJ,FRSWITCH
 ENDCARD:,XTS,COMMFLG
 7,VTM,SETPAR
 ,UZA,DEFAULT-1
 7,VTM,NEXTCARD
 ,UJ,DEFAULT-1
 NEXTCARD:,XTA,
 ,ATX,COMMFLG
 ,XTA,CARDCNT
 ,ARX,WOPAR
 ,ATX,CARDCNT
 ,ASN,64+3 .MAKCИMAЛЬHO TOЛЬKO 8 KAPT
 ,U1A,NOEND
 ,UTC,MONCARD/
 ,XTA,
 ,AEX,*CALL
 ,UZA,COPY0
 ,CALL,MONREAD*
 COPY0:14,VTM,-13
 COPY:14,XTA,MONCARD*+13
 14,UTC,MONCARD/+13
 ,ATX,
 14,VLM,COPY
 9,VTM,
 10,VTM,9
 ,CALL,PARAMT*
 14,VTM,MONCARD/
 ,ITS,14
 14,UTM,13
 ,ITS,14
  ,XTS,WOPAR
 ,CALL,PRINT8
 ,UJ,PARLOOP
 ENDCARD2:,BSS,
 SETPAR:14,VTM,TABPAR
 12,VTM,
 SETPAR1:14,XTA,
 ,UZA,SETPAR2
 14,XTA,DEFRES
 12,ATX,INPUT
 12,UTM,1
 14,UTM,4
 ,UJ,SETPAR1
 SETPAR2:,XTA,ERRORW
 ,U1A,OLDCARD
 ,XTA,INPUT
 2,UZA,
  ,XTA,INPUT
 ,ARX,MAS18
 ,AAX,MAS18
 ,AOX,CR
  ,ATX,INPUT
  ,XTA,KCOUNT*
  ,ATX,ARREAD*
  ,XTA,KCOUNT*+1
  ,ATX,ARREAD*+1
 ,XTA,WOPAR
  ,ATX,KCOUNT*+2
  ,XTA,INPUT
  ,ATX,KCOUNT*+1
  ,XTA,
  ,ATX,KCOUNT*
 2,UJ,
 ERRORF:11,VTM,ERR
 :,ITS,11
 11,UTM,4
 ,ITS,11
 ,XTS,WOPAR
 ,CALL,PRINT8
 OLDCARD:,BSS,
 ,XTA,
 ,ATX,PASS
 3,MTJ,13
 ,UJ,RETURN
 BADPAR:,BSS,
 ,UTC,*MONCRD*
 ,XTA,
 ,ATX,BADP+3
 14,VTM,BADP
 ,UJ,PRINTERR
 BADARG:,BSS,
 ,UTC,*MONCRD*
 ,XTA,
 ,ATX,BADA+3
 14,VTM,BADA
 7,VTM,FRSWITCH
 ,UJ,PRINTERR
 BADDEL:,BSS,
 11,UTM,56B
 ,ITA,11
 ,ASN,64-40
 ,ATX,BADD+3
 14,VTM,BADD
 ,XTA,
 ,ATX,COMMFLG
 7,VTM,RECOVERY
 PRINTERR:,XTA,WOPAR
 ,ATX,ERRORW
 ,ITS,14
 14,UTM,4
 ,ITS,14
 ,XTS,WOPAR
 ,CALL,PRINT8
 7,UJ,
  NOEND:,BSS,
   11,VTM,NOE
 ,UJ,ERRORF+1
 STEXTPTR:,,
 ,Z00,SNAME-1
C*
C*    CTPYKTYPA TAБЛИЦЫ  TABPAR
C*
C*    1: ИMЯ ПAPAMETPA B ISO
C*    2: TИП ПAPAMETPA
C*       0 - OШИБOЧHЫЙ ПAPAMETP
C*       1 - TИПA LIST
C*       2 - TИПA MODEL
C*       3 - TИПA STEXT
C*       4 - TИПA INPUT
C*       5 - TИПA PAGE
C*    3: ЗHAЧEHИE ПAPAMETPA ПPИ
C*       COKPAЩEHHOЙ ЗAПИCИ
C*    4: ЗHAЧEHИE ПAPAMETPA ПO
C*       YMOЛЧAHИЮ
C*
 TABPAR:,ISO,6HINPUT
 ,LOG,4
 ,LOG,03 0000
 ,LOG,
C
C     BHИMAHИE! HACTPOЙKA HA ДЛИHY CTPAHИЦЫ
C     YCTAHOBЛEHO 66 CTPOK.
C     MEHЯTЬ ЗДECЬ И B MADLENI*
C     (B EГO MACCИBE PRINTBF )
C
 :,ISO,6HPAGE
 ,LOG,5
 ,LOG,102
 ,LOG,102
C
 :,ISO,6HMODEL
 ,LOG,2
 ,ISO,6H******
 ,ISO,6H******
C
 :,ISO,6HDISP
 ,LOG,2
 ,ISO,6HDD****
 ,ISO,6HDD****
C
 :,ISO,6HEDIT
 ,LOG,1
 ,LOG,
 ,LOG,
C
 :,ISO,6HLIST
 ,LOG,1
 ,LOG,2
 ,LOG,1
C
 :,ISO,6HXTEXT
 ,LOG,4
 ,LOG,31 0000
 ,LOG,
C
 :,ISO,6HSTEXT
 ,LOG,3
 ,TEXT,8HSYSTEXT
 ,LOG,
C
 :,ISO,6HSTEXT
 ,LOG,3
 ,TEXT,8HSYSTEXT
 ,LOG,
C
 :,ISO,6HSTEXT
 ,LOG,3
 ,TEXT,8HSYSTEXT
 ,LOG,
C
 :,ISO,6HSTEXT
 ,LOG,3
 ,TEXT,8HSYSTEXT
 ,LOG,
C
 :,ISO,6HSTEXT
 ,LOG,3
 ,TEXT,8HSYSTEXT
 ,LOG,
C
 :,ISO,6HSTEXT
 ,LOG,3
 ,TEXT,8HSYSTEXT
 ,LOG,
C
 :,ISO,6HSTEXT
 ,LOG,3
 ,TEXT,8HSYSTEXT
 ,LOG,
C
 ,ISO,6H
 ,BSS,3
C
 ENDTAB: ,OCT, .!!!
C
 S20:,OCT,
 CARDCNT:,OCT,
 MASK:,OCT,
 PARAM:,OCT,
 COMMFLG:,OCT,
 USEBIT:,OCT,4
 STAR:,LOG,52
 ERR:,ISO,30H***ILLEGAL CONTROL CARD
 NOE:,ISO,30H***NO CONTROL CARD TERMINATOR
 BADP:,ISO,30H***BAD KEYWORD
 BADA:,ISO,30H***BAD ARGUMENT
 BADD:,ISO,30H***BAD DELIMITER
 BADT:,ISO,30H***BAD SYSTEM TEXT
 SOLONG:,ISO,30H***TABLES OVERFLOW  STEXT DROP
 DONT:,OCT,
 SP6:,ISO,6H
  L:,OCT,
  C77:,OCT,77
 CR:,OCT,001
 MAS18:,LOG,777777
    ERRORW:,OCT,
  SHIFS:,OCT,
  C377:,OCT,776
 C6*177:,ISO,6H'177''177''177''177''177''177'
 C6*200:,ISO,6H'200''200''200''200''200''200'
 CM4:,OCT,4
   WOPAR:,LOG,1
  CON15:,OCT,0000 04
 FW:,LOG,7777 7777 7777 7777
  ENDST:,OCT,
  NAME:,TEXT,8HSYSTEXT
  IDI:,TEXT,8HSYSTEXT
 RCON60:,LOG,60
 R10:,OCT,
 RCON10:,LOG,10
 RCON7:,LOG,7
 *CALL:,ISO,6H*CALL
 RCON2:,LOG,2
 CONR15:,LOG,77777
 MAINOVL:,TEXT,8HMADLENI*
 PASSWORD:,TEXT,8H MADLEN
 TESTST:,TEXT,8HS2TEXT**
 INFDEC:,,NDEC
     ,010,
 INFOUT:,,NOUT
   ,010,
 OUTPNT:,,
 ,Z00,OUTPT
 CHECKDR:,LOG,7740
 TIDI:,OCT,
 CON7776:,OCT,7776
 CONSS  :,OCT,77770000777
 XX/YY:,GOST,6H03 0
 /ZZ:,GOST,6H0  000
 DATE:,ISO,12H 00/00/00-''''
 TIME:,ISO,12H''00.00.00 ''''
 RCON3:,LOG,3
 CONR8:,LOG,377
 CONR10:,LOG,1777
 NEXTZ:,OCT,0000 0100 0000 0001
 BIT25:,OCT,00000001
 STEXTCNT:,OCT,001
 PRISTEXT:,ISO,12HSYSTEM TEXT:
 PRICOM:,ISO,6H ''''''''''
 ,BSS,6 .NAME+DATE+TIME
 COMMENT:,BSS,14
 1,SET,CONSS
 4096,,IDTAB
   ,END,
